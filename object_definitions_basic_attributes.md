# Basic attributes to create an object definition #

<a href='http://creativecommons.org/licenses/by-sa/3.0/'><img src='http://i.creativecommons.org/l/by-sa/2.0/uk/88x31.png' alt='Licence Creative Commons' /></a> author : Marc ALCARAZ ([ekameleon](https://code.google.com/u/109962507657971592081/))



| **id** (required) | The unique identifier of the object definition. This identifier is a key used to find the reference of the corresponding object in the IoC container. |
|:------------------|:------------------------------------------------------------------------------------------------------------------------------------------------------|
| **type** (required) | The full name of the type(class) of the object created and returned by the factory.                                                                   |
| **arguments**     | A list (Array) of all the arguments in the constructor of the class or also in the methods invoked to initialize the new object.                      |
| **properties**    | A list (Array) of all properties initialized after the creation of the object.                                                                        |
| **singleton**     | Defines soon the scope "singleton" of the object generated by the factory.                                                                            |
| **scope**         | Defines exactly the scope of the new object generated by the factory : "prototype" or "singleton".                                                    |
| **lazyInit**      | In general all "**singleton**" objects are initialized at startup in the factory. To stop such unnecessary initialization we use lazy initialization which create the **singletons** when it if first requested. |

## 1 - The "id" attribute (required) ##

This identifier is the "**unique key**" required to defines a new **object definition** in the **IoC** container.

**example**

```
{ 
    id : "user" ,
    …
```

Now look at how to use the unique identifier of an object definition to create and get the corresponding object reference in the factory.

```
var user:User = factory.getObject( "user" ) as User ;
```

The "**id**" attribute is essential to create or to get an object with the **getObject()** method.

**Hint** : I often use in my projects enumerations with a specific class who contains static constants who define a list of all important object definitions registered in the factory.

For example we can create a list of the main views of an application by quickly creating a small class **DisplayList** :

```
package example
{
    public class DisplayList
    {
        public static const BACKGROUND:String = "background" ;
        
        public static const BODY:String = "body" ;
        
        public static const MENU:String = "menu" ;
    }
}
```

Then we can create an **IoC** configuration context with the **object definitions** :

```
var objects:Array =
[
    {
        id    	: "background"  ,
        type  	: "flash.dislay.Sprite" ,
        singleton : true 
    }
    ,
    {
        id    	: "body"  ,
        type  	: "flash.dislay.Sprite" ,
        singleton : true 
    }
    ,
    {
        id    	: "menu"  ,
        type  	: "flash.dislay.Sprite" ,
        singleton : true 
    }
] ;
```

To call at any time a singleton object defined above, simply type the following code :

```
import system.ioc.factory ;
import example.DisplayList ;
import flash.display.Sprite ;

factory.run( objects ) ;

var background:Sprite = factory.getObject( DisplayList.BACKGROUND ) as Sprite ;
```

## 2 - The "type" attribute (required) ##

This attribute is mandatory and usually indicates the full name of the **class** (or type) of the object that you want to create or target in the factory (in the case of a "**singleton**" reference).

```
    type : "examples.core.User" ,
```

Make sure that the class exists in the main file of the application. If this class does not exist, it is possible to include this class (or graphics,sounds, etc..) Using the engine to load external resources of the factory IoC common. Otherwise it will force the existence of this class by calling at least once in the main application code.

Note that the expression (**String**) of this attribute can be subjected to filtering and formatting using tools passed in the configuration of the factory. These assessments and filtering can greatly simplify the writing of your **object definitions**.

**Note :**  With an **object definition** who defines a custom strategy who changes the classical strategy used by default in the lightweight container, the object generated with this custom strategy can be defines with a _different_ type from that defined in the definition but will keep a "casting" compatible with the class set with the attribute "**type**".

The object must **implements** or **extends** the type (interface or class) defined in this **object definition**. The key word "**as**" is used in the factory just after the creation of a new object and ensures that the object created has the correct type. In case of problems and incompatibilities on typing defined in the object definition with the type of the object created by the factory, the container will return a **null** object.

## 3 - The "arguments" attribute ##

The attribute "**arguments**" is a list (**Array**) of generic objects who defines the parameters that you want to inject in the constructor function for a class or a method.

This attribute can be used at the root of an object definition, then it corresponds to the parameter list of the constructor function of the class used to create the object, otherwise it match the argument list the you want to inject in a setter method (eg in the attribute "**properties**").

This attribute is optional, any method or constructor function defined without this attribute can be invoked with no parameters.

This attribute allow you to create and initialize **system.ioc.ObjectArgument** objects in the factory.

This class includes mainly the following members :

  * **value** : Refers to the value or reference of the argument. This attribute can either be the value of the argument, an expression (String) to be interpreted by the factory.
  * **policy** : Defines the strategy used by the factory to create the argument, it determines if the argument is defined with a single value, a reference to another object definition, etc.
  * **evaluators** : (optional) Set in a very special collection of objects of evaluation that will be used to filter or transform the value or the reference.

To transform a simple list (**Array**) of generic objects into a list (**Array**) of **ObjectArgument** objects the framework use a single function :

**usage**

```
system.ioc.builders.createArguments( init:Object )
```

**example**

```
import system.ioc.builders.createArguments ;

import system.ioc.ObjectArgument ;
import system.ioc.ObjectAttribute ;

var list:Array =
[
    { value  : "hello world"  } ,
    { ref    : "my_id"        } ,
    { config : "config.value" } ,
    { locale : "locale.value" } ,
    { value  : "hi world" , evaluators:["myEvaluator"] } 
] ;

var args:Array = createArguments( list ) ; 

trace( "args: " + args ) ;

var arg:ObjectArgument = args[4] as ObjectArgument ;

trace( "value:" + arg.value + " evaluators:" + arg.evaluators ) ;
```

To respect the "**hollywood principle**" this function is encapsuled and used by the framework when you initialize your **object definitions** in the factory with the **run()** method.

The **generic objects** must absolutely be defined with a mandatory attribute specific to be transformed into an **ObjectArgument** object otherwise it will not be included in the method **createArguments()** and will be replaced by a **null** value.

We can see in the example above that all objects are defined with a specific attribute :
  * value
  * ref
  * config
  * local

These attributes correspond to the possible values ​​of the attribute "**policy**" of the class **ObjectArgument**. The static enumeration defines with the **system.ioc.ObjectAttribute** class defines all the policies accepted in the  **ObjectArgument** class :

  * ObjectAttribute.VALUE 	  : "value"
  * ObjectAttribute.REFERENCE : "ref"
  * ObjectAttribute.CONFIG	  : "config"
  * ObjectAttribute.LOCALE	  : "locale"

Each of the constants defines above provide the necessary information to generate a new argument with a specific "policy" in the container.

Now, let the main example of this chapter by isolating the "**arguments**" attribute in the object definition "**user**".

```
{ 
    id         : "user" ,
    type       : "test.User" ,
    arguments  :
    [
        { value : "eKameleon" } ,
        { value : "ALCARAZ"   } ,
        { ref   : "address"   }
    ]
}
```

The arguments are defined by generic objects which usually contain an attribute "**value**" or attribute "**ref**". Only one of these attributes can be used for a single generic object. These attributes are used to define the value of an argument by value or reference to target a specific object definition in the **IoC** container.

The above example can be translate in **ActionScript** with the code :

```
var user:User = new User("eKameleon","ALCARAZ",factory.getObject("address")) ;
```

Used in the attribute "**properties**", the attribute "**arguments**" will be considered by the container only if the property defined in one of the generic objects in the collection will be of type "**Function**" (method defined in the factory).

**example**

```
properties :
[
    { name:"setMail" , arguments:[ { value:"ekameleon[at]gmail.com" } ] }
]
```

This very simple example can set the attribute "**properties**" with a method "**setMail**" that will be invoked during the initialization process of the new object in the factory.

**Note**

We can replace the classical attributes "**value**" and "**ref**" with two specials attributes : "**config**" or "**locale**". This attributes are used to evaluate the argument and to retrieve a value localized or set in the config object of the application.

### 3.1 - The "value" attribute. ###

This attribute in the arguments definitions is the most simple.

```
    { value : "eKameleon" } ,
```

This attribute defines an argument with a simple value of any type.

### 3.2 - The "ref" attribute. ###

This attribute defines a String expression who target a unique identifier register in the container.

```
    { ref : "address" } ,
```

This attribute defines the dependency injection between the objects defined in the container.

In our example the reference "**address**" will be use to create the dependency with the method **getObject("address")** of the factory and to initialize the object who needs it to exist.

All the "**ref**" attributes in the **object definitions** are the same operation mode. I will try here to generalize the use of this attribute.

This attribute is set to an expression (String) evaluated in the factory for a **system.ioc.evaluators.ReferenceEvaluator** instance.

The **ReferenceEvaluator** class target a specific **IoC** factory and analyzes a **String** expression to identify an **object definition** register in the factory and return immediately the reference of the corresponding object.

If the expression is defined with a dot notation, it will be possible to target an object in the factory and get the value of one of its members, example :

```
    { ref : "address.city" }
```

In our example the argument target the reference defines with the expression **"address.city"** and returns the value **"Marseille"**.

It's possible to use a dot notation to find a value set deeper into the attributes of the object, eg: **"foo.bar.xxxx.name"**.

The "**ref**" attribute can be defined also with the "**magic expressions**", these special strings can target by reference certain important object predefined in the configuration of the **IoC** factory.

| **#config** | Targets a specic reference defines in the configuration object of the factory. |
|:------------|:-------------------------------------------------------------------------------|
| **#locale** | Targets a specic reference defines in the configuration object of the factory with a notion of localization. |
| **#this**   | Targets the local IoC factory reference of the current object definition.      |
| **#root**   | Targets the "root" reference, can be the main timeline of the current application. |
| **#stage**  | Targets the "stage" reference of the current application defines in the config of the factory. |
| **#params** | Targets the "parameters" reference of the application defines in the config of the factory. |

To properly define the **#root**, **#stage**, **#params**, **#config** and **#locale** magic expressions you must set manually the corresponding attributes in the configuration of the **IoC** factory.

We can by example write the following **ActionScript 3** code :

```
package
{
    import system.ioc.ObjectFactory ;
    import system.ioc.Parameters;

    import flash.display.Sprite ;

    public class Main extends Sprite
    {
        public function Main()
        {
            factory = new ObjectFactory() ;

            factory.config.root   = this ;
            factory.config.stage  = stage ;
            factory.config.params = new Parameters( loaderInfo.parameters ) ;

            factory.config.config = 
            {
                color : 0xFF0000 
            }
            
            factory.config.locale = 
            {
                label : "hello world"
            }
        }
        
        public var factory:ObjectFactory ;
    }
}
```

### 3.3 - The "config" attribute ###

This special attribute can be set in place of the attribute "**value**" or attribute "**ref**" to a target value set in the configuration object factory.

**usage**

```
    { config : "author" } ,
```

**example**

```
import system.ioc.factory ;
import system.ioc.ObjectConfig ;
import flash.text.TextField;

var objects:Array =
[
    {
        id        : "message_field" ,
        type      : "flash.text.TextField" ,
        properties :
        [
           { name : "text" , config : "message" }
        ]
    }
    ,
    {
        id        : "menu_field" ,
        type      : "flash.text.TextField" ,
        properties :
        [
           { name : "text" , config : "menu.title" }
        ]
    }
] ;

var setting:ObjectConfig = new ObjectConfig() ;

setting.config = 
{
   message : "hello world" ,
   menu    :
   {
        color : 0xC2C2C2 ,
        title : "my title"
   }
}

factory.config = setting ;

factory.run( objects ) ;

trace( factory.getObject( "message_field" ).text ) ;
trace( factory.getObject( "menu_field" ).text ) ;
```

In the output panel when we launch the previous code we can read :

```
hello world
my title
```

### 3.4 - The "locale" attribute ###

This special attribute target values set in a **locale** object defines in the configuration of the factory.

The process to use the **config** or the **locale** objects appear exactly the same but there is still a difference between the two objects.

The **config** object contains all global values to set your application : external service uri, display settings like colors, size, font name, etc.

The **local** object defines the content of the application who must be adapted to various languages and regions contains for example the label expressions, messages, sound references, video links and uris with an i18n setting.

In the **IoC factory** the container target a basic **Object** reference but it's really simple to use it with a complex **i18n** engine.

**usage**

```
    { locale : "message" } ,
```

**example**

```
import system.ioc.factory ;
import system.ioc.ObjectConfig ;
import flash.text.TextField;

var objects:Array =
[
    {
        id        : "message_field" ,
        type      : "flash.text.TextField" ,
        properties :
        [
           { name : "text" , locale : "message" }
        ]
    }
    ,
    {
        id        : "menu_field" ,
        type      : "flash.text.TextField" ,
        properties :
        [
           { name : "text" , locale : "menu.title" }
        ]
    }
] ;

var setting:ObjectConfig = new ObjectConfig() ;

setting.locale = 
{
   message : "hello world" ,
   menu    :
   {
        color : 0xC2C2C2 ,
        title : "my title"
   }
}

factory.config = setting ;

factory.run( objects ) ;

trace( factory.getObject( "message_field" ).text ) ;
trace( factory.getObject( "menu_field" ).text ) ;
```

In the output panel when we launch the previous code we can read :

```
hello world
my title
```

### 3.5 - The "evaluators" attribute ###

This optional attribute can be defines in the "arguments" definitions. We can learn more about this attribute in the end of this chapter.

## 4 - The "properties" attribute ##

In an **object definition** the attribute "**properties**" is a collection (**Array**) of **generic objects** who define the attributes or invoke some of the methods of the object generated with the current **object definition**.

**example**

```
properties :
[
    { name:"age"     , value     : 32        } ,
    { name:"job"     , ref       : "job_dev" } ,
    { name:"setMail" , arguments : [ { value:"ekameleon[at]gmail[dot]com" } ] }
]
```

The generic objects defined in the collection above require a "**name**" attribute that indicates what attribute we want to initialize.

Then we can set the value of an attribute using the attribute "**value**", "**ref**" , "**config**", "**locale**" and in the case of a method we can use the optional attribute "**arguments**" to determinate if required parameters.

**Note :** the #init magic expression

It's possible to use the name attribute with the magic value **#init** to initialize the object with a **for..in** loop.

**example**

```
import system.ioc.factory;
import system.ioc.ObjectConfig;

import flash.display.Sprite;
import flash.text.TextFormat;

var config:Object = 
{
    field : 
    {
        defaultTextFormat : new TextFormat("Arial",12,0xFFFF00) ,
        selectable        : false ,
        height            : 250 ,
        width             : 250 ,
        x                 :  25 ,
        y                 :  25 
    }
};

var locale:Object = 
{
    field : 
    {
        text : "hello world" 
    }
};
    
var setting:ObjectConfig = factory.config ;
   
setting.root   = this ;
setting.config = config ;
setting.locale = locale ;

var objects:Array =
[
    {
        id         : "field" ,
        type       : "flash.text.TextField" ,
        singleton  : true ,
        lazyInit   : true ,
        properties :
        [
            { name : "#init" , config : "field" } ,
            { name : "#init" , locale : "field" } 
        ]
    }
    ,
    {
        id               : "application" ,
        type             : "flash.display.Sprite" ,
        factoryReference : "#root" ,
        singleton        : true ,
        properties       :
        [
            { name : "addChild"   , arguments  : [ { ref : "field" } ] } 
        ]
    }
] ;

factory.run( objects ) ;
```

In the previous example the **#init** property target the **"field"** member in the **config** and **locale** objects defines in the configuration of the factory.

The factory use a basic **for..in** loop to copy all members defines in the enumerable objects in the new object generated with the current **object definition**.

We can target an enumerable object with the **value**, **ref**, **config**, and **locale** attributes.

This technique facilitates the initialization of the objects and allows to copy multiple values with one line. The **object definitions** are simplified and more readable with this notation.

### 4.1 - Use the "value" attribute ###

```
    { name : "age" , value : 32 } ,
```

This example sets the attribute with a simple value of any type.

### 4.2 - Use the "ref" attribute ###

The attribute "**ref**" is set to a string (String), which mainly targets a specific **object definition** in the **IoC** container.

```
    { name : "job"  , ref : "job_dev" } ,
```

As in the definitions of the attribute "**arguments**", it is possible to go further in defining the expression of the attribute "**ref**". This expression is evaluated by the factory when creating the property value, so it is possible to use a multi-level dot notation to retrieve the value of a property defined in the reference target in the container.

**example**

```
{
    id     	: "job_dev"   ,
    type   	: "test.Job"  ,
    properties : [ { name:"name" , value:"AS Developper" } ]
}
,
{
    id         : "field"   ,
    type       : "flash.text.TextField"  ,
    properties :
    [
        { name : "autoSize" , value : "left"         } ,
        { name : "text"     , ref   : "job_dev.name" }
    ]
}
```

This simple example display the name of the "job" defined in the definition "**job\_dev**" in a simple dynamic text field. The attribute "**ref**" is set to an expression with a dot notation, the first element in this expression corresponds to the identifier of an object definition in the factory and once this reference identified and recovered in the factory property "text" of the text field takes the value of the **name** property of the **examples.core.Job** object.

### 4.3 - The "config" and "locale" attribute ###

See the description of these two attributes in the sections **3.3** and **3.4** previously seen.

**usage**

```
{ name : "name"  , config : "author" } ,
```

or

```
{ name : "name"  , locale : "author" } ,
```

### 4.4 - The "evaluators" attribute ###

We will see more precisely in the special chapter dedicated to how to use this attribute.

**usage**

```
{ 
    name       : "date"  , 
    value      : "new Date(2008,7,30,16,00,00);" , 
    evaluators : [ ...evaluators ]
} ,
```

## 5 - The "scope", "singleton" and "lazyInit" attributes ##

When we create an **object definition** we set up a kind of **recipe** for creating an object in the application.

The idea of ​​**recipe** is important, it indicates that an **object definition** to create a large number of instances in the application using strategies of varying complexity of creation and initialization. It is therefore possible to create multiple objects with a single **object definition**.

These items have specific life cycles and sometimes will have a single or multiple representation in the application.

The notion of scope is important in an **IoC** container as it indicates the factory how to create the application objects and especially how to reference them later.

### 5.1 - The "scope" attribute ###

The **scope** attribute is optional in the object definitions and takes  a string value.

```
    scope : "prototype" ,
```

The **scope** attribute can be set with the string values **"prototype"** or **"singleton"**.

The scope with the value **"prototype"** indicates that the **IoC** factory can create with the same **object definition** any number of instances.

```
import system.ioc.factory ;

import examples.core.User ;

var user1:User = factory.getObject("user") as User 
var user2:User = factory.getObject("user") as User ;

trace( user1 == user2 ) ; // false
```

The scope with the value **"singleton"** indicates that the **object definition** can create a single instance and then stores it in the container. Thereafter each call that object definition will return a simple reference to the singleton register in the cache of the container.

By default, all object definitions have the scope with the value **"prototype"** but it is possible to change this value with the values defined above.

You can find in the **system.ioc.ObjectScope** class the static enumeration of all scopes defines in the **IoC** factory :

  * ObjectScope.PROTOTYPE = "prototype"
  * ObjectScope.SINGLETON = "singleton"

**Note :** The **scope** attribute always overwrites the value of the **singleton** attribute in the same object definition if it is used.

### 5.2 - The singleton attribute ###

This attribute is a boolean option and allows to quickly change the default state of the **scope** attribute by assigning the value **"singleton"**.

This attribute is sufficient to quickly transform the nature of the current **object definition**.

```
    singleton : true ,
```

If the **object definition** is defines with the scope **"singleton"** then the object instance create via the factory is stored in the cache of the container and will be used later to each call in the factory corresponding to it.

If for a specific identifier in the **IoC** factory, the object definition is initialized as **"singleton"** after each call to this identifier (**id**) in the **getObject()** method of the factory will return a singleton reference only. The first time the **getObject()** method is invoked, the unique reference is created, initialized and stored in memory in the factory.

**example**

```
import system.ioc.factory;

import examples.core.User ;

var objects:Array =
[
    {
        id         : "user" ,
        type       : "examples.core.User" ,
        singleton   : true
        arguments  :
        [
            { value : "eKameleon" } ,
            { value : "ALCARAZ"   } ,
            { ref   : "address"   }
        ]
    }
];

var user1:User = factory.getObject("user") as User ;
var user2:User = factory.getObject("user") as User ;

trace( user1 == user2 ) ; // true
```

### 5.3 - The "lazyInit" attribute ###

This attribute is a boolean option in the **object definitions**, it is set to **false** by default.

```
    lazyInit : true , // only if this object is a singleton
```

This attribute only applies on the object definitions with a scope **"singleton"**. If you use this attribute to a non-singleton object (**"prototype"**), this attribute will be simply ignored.

If this attribute is **false** or not defined, the **singleton** object will be automatically instantiated during the initialization of the factory.

If this attribute is **true** then the singleton object is not instantiated automatically and the singleton object is created only if the **getObject()** method is invoked a first time in the application after the container initialization.

In the life cycle of the application it may be convenient to create the singleton objects at startup. This singletons can initiate certain processes automatically without having to use a particular code in the application. This feature will also place certain objects in memory that will be available much fate thereafter.

The **lazyInit** attribute can be very important if a singleton object defined in the factory should absolutely not be instantiated at application startup. We can note in this regard the following reasons :

  * Do not overload the memory with a singleton not important in the life cycle of the application at startup.
  * The dependencies of the singleton not exist or can't be instantiated in the IoC factory yet.