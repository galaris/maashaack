/*
  Version: MPL 1.1/GPL 2.0/LGPL 2.1
 
  The contents of this file are subject to the Mozilla Public License Version
  1.1 (the "License"); you may not use this file except in compliance with
  the License. You may obtain a copy of the License at
  http://www.mozilla.org/MPL/
  
  Software distributed under the License is distributed on an "AS IS" basis,
  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
  for the specific language governing rights and limitations under the
  License.
  
  The Original Code is [maashaack framework].
  
  The Initial Developers of the Original Code are
  Zwetan Kjukov <zwetan@gmail.com> and Marc Alcaraz <ekameleon@gmail.com>.
  Portions created by the Initial Developers are Copyright (C) 2006-2011
  the Initial Developers. All Rights Reserved.
  
  Contributor(s):
  
  Alternatively, the contents of this file may be used under the terms of
  either the GNU General Public License Version 2 or later (the "GPL"), or
  the GNU Lesser General Public License Version 2.1 or later (the "LGPL"),
  in which case the provisions of the GPL or the LGPL are applicable instead
  of those above. If you wish to allow use of your version of this file only
  under the terms of either the GPL or the LGPL, and not to allow others to
  use your version of this file under the terms of the MPL, indicate your
  decision by deleting the provisions above and replace them with the notice
  and other provisions required by the LGPL or the GPL. If you do not delete
  the provisions above, a recipient may use your version of this file under
  the terms of any one of the MPL, the GPL or the LGPL.
*/

<languageVersion: 1.0;>

kernel Magnify
<   
    namespace   : "graphics.filters";
    vendor      : "ALCARAZ Marc (eKameleon)";
    version     : 1 ;
    description : "Creates a magnified circle around the specific center point. Note: If the outerRadius is larger than the innerRadius, the magnification beween these regions will be interpolated along a cosine curve. You may set innerRadius to 0 to create a 'bubble' effect, or set it equal to or greater than outerRadius for a precise circle of magnification." ; 
>
{
    /**
     * The center position of the magnification.
     */
    parameter float2 center
    <
        minValue     : float2(    0.0 ,    0.0 ) ;
        maxValue     : float2( 2048.0 , 2048.0 ) ;
        defaultValue : float2(    0.0 ,    0.0 ) ;
    >;
    
    /**
     * The radius of the inner area.
     */
    parameter float innerRadius
    <
        minValue     :   0.0 ;
        maxValue     : 500.0 ;
        defaultValue :  50.0 ;
    >;
    
    /**
     * The amount of magnification.
     */
    parameter float magnification
    <
        minValue     :  1.0 ;
        maxValue     : 50.0 ;
        defaultValue :  5.0 ;
    >;  
    
    /**
     * The radius of the outer area, specify the pixels between the innerRadius and outerRaius will be partly magnified
     */
    parameter float outerRadius
    <
        minValue     :   0.0 ;
        maxValue     : 500.0 ;
        defaultValue :  80.0 ;
    >;
    
    input  image4 source ;
    output pixel4 result ;
    
    void evaluatePixel()
    {
        float2 target ;
        
        float2 coord   = outCoord();
        float2 vector  = coord - center;
        float distance = length( vector ) ;
        
        if ( distance < outerRadius )
        {
            float2 maximum = center + ( vector / magnification ) ;
            
            if( distance < innerRadius )
            {
                target = maximum ;
            }
            else
            {
                float ratio     = (distance - innerRadius) / (outerRadius - innerRadius) ;
                float trigRatio = ( cos( ratio * 3.14159265258979 ) + 1.0 ) / 2.0 ;
                
                target = maximum * ( trigRatio ) + coord * ( 1.0 - trigRatio ) ;
            }
        }
        else
        {
            target = coord;
        }
        
        result = sampleLinear( source , target ) ;
    }
}