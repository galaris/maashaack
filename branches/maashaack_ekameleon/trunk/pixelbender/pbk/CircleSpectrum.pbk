/*
  Version: MPL 1.1/GPL 2.0/LGPL 2.1
 
  The contents of this file are subject to the Mozilla Public License Version
  1.1 (the "License"); you may not use this file except in compliance with
  the License. You may obtain a copy of the License at
  http://www.mozilla.org/MPL/
  
  Software distributed under the License is distributed on an "AS IS" basis,
  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
  for the specific language governing rights and limitations under the
  License.
  
  The Original Code is [maashaack framework].
  
  The Initial Developers of the Original Code are
  Zwetan Kjukov <zwetan@gmail.com> and Marc Alcaraz <ekameleon@gmail.com>.
  Portions created by the Initial Developers are Copyright (C) 2006-2011
  the Initial Developers. All Rights Reserved.
  
  Contributor(s):
  
  Alternatively, the contents of this file may be used under the terms of
  either the GNU General Public License Version 2 or later (the "GPL"), or
  the GNU Lesser General Public License Version 2.1 or later (the "LGPL"),
  in which case the provisions of the GPL or the LGPL are applicable instead
  of those above. If you wish to allow use of your version of this file only
  under the terms of either the GPL or the LGPL, and not to allow others to
  use your version of this file under the terms of the MPL, indicate your
  decision by deleting the provisions above and replace them with the notice
  and other provisions required by the LGPL or the GPL. If you do not delete
  the provisions above, a recipient may use your version of this file under
  the terms of any one of the MPL, the GPL or the LGPL.
*/

<languageVersion : 1.0;>

kernel CircleSpectrum
<
    namespace   : "graphics.filters";
    vendor      : "ALCARAZ Marc (eKameleon)";
    version     : 1 ;
    description : "Maps a color spectrum to a circle area.";
>
{
    input  image4 source ;
    output float4 result ;
    
    /**
     * The center position of the circle.
     */
    parameter float2 center
    <
        minValue     : float2(    0.0 ) ;
        maxValue     : float2( 2800.0 ) ;
        defaultValue : float2(  100.0 ) ;
    >;
    
    /**
     * The inner radius value.
     */
    parameter float innerRadius
    <
        minValue     :    0.0 ;
        maxValue     : 2800.0 ;
        defaultValue :   50.0 ;
    >;
    
    /**
     * The outer radius value.
     */
    parameter float outerRadius
    <
        minValue     :    0.0 ;
        maxValue     : 2800.0 ;
        defaultValue :  100.0 ;
    >;
    
    /**
     * The ratio of the spectrum value.
     */ 
    parameter float ratio
    <
        minValue     : 0.0 ;
        maxValue     : 1.0 ;
        defaultValue : 1.0 ;
    >;
    
    /**
     * The rotation angle value.
     */
    parameter float rotation
    <
        minValue     :   0.0 ;
        maxValue     : 360.0 ;
        defaultValue :   0.0 ;
    >;
    
    /**
     * Evaluates the current pixel.
     */
    void evaluatePixel()
    {
        float2 pos      = outCoord() ;
        float2 distance = pos - center ;
        float  radius   = length( distance ) ;
        pixel4 color    = sampleLinear( source , pos );
        
        // calculate rotation based on the distance to the center
        
        float angle = degrees( atan( distance[1] , distance[0] ) ) ;
        
        angle = mod( angle + ( 360.0 - rotation ) , 360.0 ) ;
        
        // pixel is in range
        
        if (radius > innerRadius - 1.0 && radius < outerRadius + 1.0 ) 
        {
            angle = radians(angle / ratio);
            
            result.r = ( cos( angle ) * 127.0 + 128.0 ) / 255.0 ;
            result.g = ( cos( angle + radians( 120.0 ) ) * 127.0 + 128.0 ) / 255.0 ;
            result.b = ( cos( angle + radians( 240.0 ) ) * 127.0 + 128.0 ) / 255.0 ;
            result.a = 1.0 ;
            
            // soften pixel that are less than a pixel away from radius
            
            float factor = 0.0 ;
            
            if ( radius < innerRadius ) 
            {
                factor = innerRadius - radius ;
            }
            else if ( radius > outerRadius ) 
            {
                factor = radius - outerRadius ;
            }
            
            result = mix( result , color, factor );
        }
        else
        {
            result = color;
        }
    }
}
