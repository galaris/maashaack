/*
  Version: MPL 1.1/GPL 2.0/LGPL 2.1
 
  The contents of this file are subject to the Mozilla Public License Version
  1.1 (the "License"); you may not use this file except in compliance with
  the License. You may obtain a copy of the License at
  http://www.mozilla.org/MPL/
  
  Software distributed under the License is distributed on an "AS IS" basis,
  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
  for the specific language governing rights and limitations under the
  License.
  
  The Original Code is [maashaack framework].
  
  The Initial Developers of the Original Code are
  Zwetan Kjukov <zwetan@gmail.com> and Marc Alcaraz <ekameleon@gmail.com>.
  Portions created by the Initial Developers are Copyright (C) 2006-2011
  the Initial Developers. All Rights Reserved.
  
  Contributor(s):
  
  Alternatively, the contents of this file may be used under the terms of
  either the GNU General Public License Version 2 or later (the "GPL"), or
  the GNU Lesser General Public License Version 2.1 or later (the "LGPL"),
  in which case the provisions of the GPL or the LGPL are applicable instead
  of those above. If you wish to allow use of your version of this file only
  under the terms of either the GPL or the LGPL, and not to allow others to
  use your version of this file under the terms of the MPL, indicate your
  decision by deleting the provisions above and replace them with the notice
  and other provisions required by the LGPL or the GPL. If you do not delete
  the provisions above, a recipient may use your version of this file under
  the terms of any one of the MPL, the GPL or the LGPL.
*/

<languageVersion : 1.0;>

kernel RGBDistort
<   
    namespace   : "graphics.filters";
    vendor      : "ALCARAZ Marc (eKameleon)";
    version     : 1 ;
    description : "Distorts all RGB pixels." ;
>
{
    /**
     * Specify the direction of the effect.
     */
    parameter float3 direction
    <
        minValue     : float3( -1.0 , -1.0 , -1.0 ) ;
        maxValue     : float3(  1.0 ,  1.0 ,  1.0 ) ;
        defaultValue : float3( -1.0 ,  0.0 ,  1.0 ) ;
    >;
    
    /**
     * Specify the frequency of the effect.
     */
    parameter float3 frequency
    <
        minValue     : float3( 0.0 , 0.0 , 0.0 ) ;
        maxValue     : float3( 1.0 , 1.0 , 1.0 ) ;
        defaultValue : float3( 0.1 , 0.1 , 0.1 ) ;
    >;
    
    /**
     * Specify the intensity of the effect.
     */
    parameter float3 intensity
    <
        minValue     : float3(  0.0 , 0.0 , 0.0 ) ;
        maxValue     : float3(  1.0 , 1.0 , 1.0 ) ;
        defaultValue : float3(  0.5 , 0.0 , 0.7 ) ;
    >;
    
    input  image4 source ;
    output pixel4 result ;

    void evaluatePixel()
    {
        float2 pos = outCoord();
        
        result = sampleLinear( source , pos ) ;
        
        float3 ratio = intensity * 10.0 ;
        
        float2 dR = float2( pos.x - ( ratio.r * direction.r ) + ( sin( pos.y * frequency.r ) * ratio.r ) , pos.y ) ;
        float2 dG = float2( pos.x - ( ratio.g * direction.g ) + ( sin( pos.y * frequency.g ) * ratio.g ) , pos.y ) ;
        float2 dB = float2( pos.x - ( ratio.b * direction.b ) + ( sin( pos.y * frequency.b ) * ratio.b ) , pos.y ) ;
        
        pixel4 red   = sampleLinear( source , dR ) ;
        pixel4 green = sampleLinear( source , dG ) ;
        pixel4 blue  = sampleLinear( source , dB ) ;
        
        result.r = red.r   ;
        result.g = green.g ;
        result.b = blue.b  ;
        result.a = clamp( red.a + green.a + blue.a , 0.0 , 1.0 );
    }
}