/*
  Version: MPL 1.1/GPL 2.0/LGPL 2.1
 
  The contents of this file are subject to the Mozilla Public License Version
  1.1 (the "License"); you may not use this file except in compliance with
  the License. You may obtain a copy of the License at
  http://www.mozilla.org/MPL/
  
  Software distributed under the License is distributed on an "AS IS" basis,
  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
  for the specific language governing rights and limitations under the
  License.
  
  The Original Code is [maashaack framework].
  
  The Initial Developers of the Original Code are
  Zwetan Kjukov <zwetan@gmail.com> and Marc Alcaraz <ekameleon@gmail.com>.
  Portions created by the Initial Developers are Copyright (C) 2006-2011
  the Initial Developers. All Rights Reserved.
  
  Contributor(s):
  
  Alternatively, the contents of this file may be used under the terms of
  either the GNU General Public License Version 2 or later (the "GPL"), or
  the GNU Lesser General Public License Version 2.1 or later (the "LGPL"),
  in which case the provisions of the GPL or the LGPL are applicable instead
  of those above. If you wish to allow use of your version of this file only
  under the terms of either the GPL or the LGPL, and not to allow others to
  use your version of this file under the terms of the MPL, indicate your
  decision by deleting the provisions above and replace them with the notice
  and other provisions required by the LGPL or the GPL. If you do not delete
  the provisions above, a recipient may use your version of this file under
  the terms of any one of the MPL, the GPL or the LGPL.
*/

<languageVersion: 1.0;>

kernel Halftone
<
    namespace   : "graphics.filters";
    vendor      : "ALCARAZ Marc (eKameleon)";
    version     : 1 ;
    description : "Halftone is the reprographic technique that simulates continuous tone imagery through the use of dots, varying either in size or in spacing.";
>
{
    input image4  source ;
    output pixel4 result ;
    
    /**
     * The angle value of the Halftone effect.
     */
    parameter float angle
    <
        minValue     :   0.0 ;
        maxValue     : 180.0 ;
        defaultValue :   0.0 ;
    >;
    
    /**
     * The pitch value of the Halftone effect.
     */
    parameter int pitch
    <
        minValue     :   0 ;
        maxValue     : 200 ;
        defaultValue :   5 ;
    >;
    
    /**
     * Indicates if the filter apply a black or white or color effect.
     */
    parameter int useColor
    <
        minValue     : 0 ;
        maxValue     : 1 ;
        defaultValue : 0 ;
    >;
       
    void evaluatePixel()
    {
        float angleRadians = radians( angle ) ;
        
        float cosine       = cos( angleRadians ) ;
        float sine         = sin( angleRadians ) ;
        
        float pitchFloat   = float(pitch);
        float dotRadiusMax = ( pitchFloat / 2.0 + 0.5 ) * 1.414214 ; // Half the diaganol of the grid
        
        float rCosTheta    = pitchFloat * cosine ;
        float rSinTheta    = pitchFloat * sine   ;
        
        float2 antiRotatedOutCoord = float2(outCoord().x * cosine - outCoord().y * -sine, outCoord().x * -sine + outCoord().y * cosine);
        float2 antiRotatedTopLeft  = pitchFloat * floor(antiRotatedOutCoord / pitchFloat);
        float2 topLeftCoord        = float2(antiRotatedTopLeft.x * cosine - antiRotatedTopLeft.y * sine, antiRotatedTopLeft.x * sine + antiRotatedTopLeft.y * cosine);
        
        float2 bottomLeftCoord  = topLeftCoord + float2(-rSinTheta, rCosTheta);
        float2 bottomRightCoord = bottomLeftCoord + float2(rCosTheta, rSinTheta);
        float2 topRightCoord    = topLeftCoord + float2(rCosTheta, rSinTheta);
        
        pixel4 topLeftPixel  = sampleNearest( source, topLeftCoord);
        float  topLeftRadius = dotRadiusMax * topLeftPixel.a * (1.0 -  (topLeftPixel.r + topLeftPixel.g + topLeftPixel.b) / 3.0) - 1.0;
        float  invAlpha      = smoothStep(0.0, 1.0, distance(outCoord(), topLeftCoord) - topLeftRadius);
        
        pixel4 topRightPixel = sampleNearest( source , topRightCoord ) ;
        float topRightRadius =  dotRadiusMax * topRightPixel.a * (1.0 -  (topRightPixel.r + topRightPixel.g + topRightPixel.b) / 3.0) - 1.0;
        
        invAlpha *= smoothStep(0.0, 1.0, distance(outCoord(), topRightCoord) - topRightRadius);

        pixel4 bottomRightPixel = sampleNearest( source , bottomRightCoord );
        float bottomRightRadius =  dotRadiusMax * bottomRightPixel.a * (1.0 -  (bottomRightPixel.r + bottomRightPixel.g + bottomRightPixel.b) / 3.0) - 1.0;
        
        invAlpha *= smoothStep(0.0, 1.0, distance(outCoord(), bottomRightCoord) - bottomRightRadius);

        pixel4 bottomLeftPixel = sampleNearest( source , bottomLeftCoord );
        float bottomLeftRadius =  dotRadiusMax * bottomLeftPixel.a * (1.0 - (bottomLeftPixel.r + bottomLeftPixel.g + bottomLeftPixel.b) / 3.0) - 1.0;
        
        invAlpha *= smoothStep(0.0, 1.0, distance(outCoord(), bottomLeftCoord) - bottomLeftRadius);
        
        pixel4 color = sampleNearest( source, outCoord());
        
        if ( useColor == 1 )
        {
            result.r = color.r ;
            result.g = color.g ;
            result.b = color.b ;
        }
        else
        {
            result.r = result.g = result.b = 0.0 ;
        }
            
        result.a = 1.0 - invAlpha;
    }
}