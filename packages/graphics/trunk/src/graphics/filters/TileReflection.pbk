/*
  Version: MPL 1.1/GPL 2.0/LGPL 2.1
 
  The contents of this file are subject to the Mozilla Public License Version
  1.1 (the "License"); you may not use this file except in compliance with
  the License. You may obtain a copy of the License at
  http://www.mozilla.org/MPL/
  
  Software distributed under the License is distributed on an "AS IS" basis,
  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
  for the specific language governing rights and limitations under the
  License.
  
  The Original Code is [maashaack framework].
  
  The Initial Developers of the Original Code are
  Zwetan Kjukov <zwetan@gmail.com> and Marc Alcaraz <ekameleon@gmail.com>.
  Portions created by the Initial Developers are Copyright (C) 2006-2011
  the Initial Developers. All Rights Reserved.
  
  Contributor(s):
  
  Alternatively, the contents of this file may be used under the terms of
  either the GNU General Public License Version 2 or later (the "GPL"), or
  the GNU Lesser General Public License Version 2.1 or later (the "LGPL"),
  in which case the provisions of the GPL or the LGPL are applicable instead
  of those above. If you wish to allow use of your version of this file only
  under the terms of either the GPL or the LGPL, and not to allow others to
  use your version of this file under the terms of the MPL, indicate your
  decision by deleting the provisions above and replace them with the notice
  and other provisions required by the LGPL or the GPL. If you do not delete
  the provisions above, a recipient may use your version of this file under
  the terms of any one of the MPL, the GPL or the LGPL.
*/

<languageVersion: 1.0;>

kernel TileReflection
<   
    namespace   : "graphics.filters";
    vendor      : "ALCARAZ Marc (eKameleon)";
    version     : 1 ;
    description : "Tile reflection filter with beautiful mirror effect - not FlashPlayer compatible for the moment :(" ; >
{
    /**
     * Specify the angle value of the filter.
     */
    parameter float angle
    <
        minValue     : -0.78539815 ;
        maxValue     :  0.78539815 ;
        defaultValue :  0.0 ;
    >;
    
    /**
     * Specify the center position of the filter.
     */
    parameter float2 center
    <
        minValue     : float2(  100.0 , 100.0  ) ;
        maxValue     : float2( 1024.0 , 1024.0 ) ;
        defaultValue : float2(  200.0 ,  200.0 ) ;
    >;
    
    /**
     * Specify the curvature value of the tile filter.
     */
    parameter float curvature
    <
        minValue     : -20.0 ;
        maxValue     :  20.0 ;
        defaultValue :   1.0 ;
    >;
    
    /**
     * Specify the squaresize value of the filter.
     */
    parameter float squaresize
    <
        minValue     :   2.0 ;
        maxValue     : 200.0 ;
        defaultValue :   5.0 ;
    >;
    
    const int aasamples = 50 ;
    
    dependent float d_sin ;
    dependent float d_cos ;
    dependent float d_scale ;
    dependent float d_curvature ;
    dependent float d_aapt_x[aasamples] ;
    dependent float d_aapt_y[aasamples] ;
    
    input  image4 source ;
    output float4 result ;

    void evaluateDependents()
    {
        d_sin = sin (angle) ;
        d_cos = cos (angle) ;

        d_scale = 3.1415926 / squaresize ;

        float t_curvature = curvature;
        
        if (t_curvature == 0.0)
        {
            t_curvature = 1.0 ;
        }
        
        d_curvature = t_curvature * t_curvature / 10.0 * (abs(t_curvature)/t_curvature) ;

        for ( int i = 0 ; i < aasamples ; i++ )
        {
            float  x = float((i * 4))/float(aasamples);
            float  y = float(i)/float(aasamples) ;
            x = x - float(int(x));
            d_aapt_x[i] = d_cos * x + d_sin * y ;
            d_aapt_y[i] = d_cos * y - d_sin * x ;
        }
    }

    void evaluatePixel()
    {
        float2 pixel = outCoord() ;
        
        float hw = center.x ;
        float hh = center.y ;
        
        float i = pixel.x - hw ;
        float j = pixel.y - hh ;

        float b = 0.0 ;
        float g = 0.0 ;
        float r = 0.0 ;
        float a = 0.0 ;
        
        for (int mm = 0 ; mm < aasamples ; mm++ )
        {
            float u = i + d_aapt_x[mm] ;
            float v = j - d_aapt_y[mm] ;

            float s =  d_cos * u + d_sin * v ;
            float t = -d_sin * u + d_cos * v ;

            s += d_curvature * tan(s * d_scale) ;
            t += d_curvature * tan(t * d_scale) ;
            
            u = d_cos * s - d_sin * t ;
            v = d_sin * s + d_cos * t ;
            
            float xSample = (hw + u) ;
            float ySample = (hh + v) ;
            
            pixel4 rgba = sampleNearest( source ,float2(xSample,ySample));
            
            r += rgba.r ;
            g += rgba.g ;
            b += rgba.b ;
            a += rgba.a ;
        }

        result = clamp(pixel4(r,g,b,a)/float(aasamples),0.0,1.0);
    }
}